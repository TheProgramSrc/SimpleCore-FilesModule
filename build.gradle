plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.kyori.blossom' version '1.2.0'
    id 'org.jetbrains.dokka' version '1.7.20'
}

def projectVersion = (System.getenv("VERSION") ?: '0.1.2-SNAPSHOT').replaceFirst("v", "").replace('/', '')

group 'xyz.theprogramsrc'
version projectVersion
description 'File configurations and utils for the SimpleCore API'

repositories {
    mavenLocal()
    mavenCentral()

    maven { url 'https://repo.theprogramsrc.xyz/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:1.7.20'
    compileOnly 'xyz.theprogramsrc:simplecoreapi:0.4.0-SNAPSHOT'

    implementation 'me.carleslc.Simple-YAML:Simple-Yaml:1.8.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

shadowJar {
    relocate 'org.simpleyaml', 'xyz.theprogramsrc.filesmodule.libs.simpleyaml'
    relocate 'org.yaml.snakeyaml', 'xyz.theprogramsrc.filesmodule.libs.snakeyaml'

    mergeServiceFiles()
    exclude('**/*.kotlin_metadata')
    exclude('**/*.kotlin_builtins')

    archiveBaseName.set('FilesModule')
    archiveClassifier.set('')
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
}

blossom {
    replaceToken '@name@', rootProject.name
    replaceToken '@version@', project.version.toString()
    replaceToken '@description@', project.description
    replaceToken '@git_short@', System.getenv("GIT_COMMIT_SHORT_HASH") ?: "unknown"
    replaceToken '@git_full@', System.getenv("GIT_COMMIT_LONG_HASH") ?: "unknown"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
    testImplementation {
        extendsFrom(compileOnly)
    }
}

tasks.named("dokkaHtml") {
    outputDirectory.set(file(project.buildDir.absolutePath + '/dokka'))
}

publishing {
    repositories {
        if(System.getenv('env') == 'prod'){
            maven {
                name = 'TheProgramSrcRepository'
                credentials.username = System.getenv('NEXUS_USERNAME')
                credentials.password = System.getenv('NEXUS_PASSWORD')
                url = uri("https://repo.theprogramsrc.xyz/repository/simplecoreapi-modules/")
            }
        }else{
            mavenLocal()
        }
    }
    publications {
        mavenKotlin(MavenPublication) { publication ->
            artifactId = 'filesmodule'

            from components.java

            pom.withXml {
                asNode().appendNode('packaging', 'jar')
                if(asNode().get('dependencies')[0] != null) {
                    asNode().remove(asNode().get('dependencies')[0])
                }
            }
        }
    }
}

publish.dependsOn clean, test, jar, shadowJar